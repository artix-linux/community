# Maintainer: artoo <artoo@artixlinux.org>

pkgname=calamares
pkgver=3.2.2
pkgrel=6
pkgdesc='Distribution-independent installer framework'
arch=('x86_64')
license=(GPL)
url="https://github.com/artix-linux/calamares"
license=('LGPL')
depends=('yaml-cpp' 'kpmcore>=3.1.0' 'boost-libs' 'hwinfo' 'gtk-update-icon-cache'
         'plasma-framework' 'libpwquality' 'pythonqt' 'polkit-qt5')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'boost')
optdepends=('calamares-branding: Artix branding')
source=("${url}/archive/v$pkgver.tar.gz"
        0001-artix-default-config.patch
        0001-postcfg-module.patch
        0001-services-runit-module.patch)
sha256sums=('db04dbe4a577438ec59b61eeb7ab23421c291d86795064dc1c437d09aad81a92'
            '459b38135c147fa095f72dd91ae4e481a03f6eae9b4e8a6018f2d593da54a285'
            '5a96a6631b7b31e991fc037a23b3aa95c7951dbd3eef25fc4d0ccce4aebed8f8'
            '1833271a4c132ba4e030272bce253bf2d42a82071a1953426e6f4cbc51b70a3f')

prepare() {
    cd $pkgname-$pkgver
    # patches here

    patch -Np 1 -i $srcdir/0001-postcfg-module.patch
    patch -Np 1 -i $srcdir/0001-services-runit-module.patch
    patch -Np 1 -i $srcdir/0001-artix-default-config.patch

}

build() {
	cd $pkgname-$pkgver

	mkdir -p build
	cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DWITH_PYTHONQT:BOOL=ON \
              -DINSTALL_CONFIG:BOOL=ON \
              -DINSTALL_POLKIT:BOOL=ON \
              -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg dracut dracutlukscfg \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt"
        make
}

package() {
	cd $pkgname-$pkgver/build
	make DESTDIR="$pkgdir" install

    cd ${srcdir}/$pkgname-$pkgver

    install -dm755 ${pkgdir}/etc/calamares/modules
    install -m644 data/artix/settings.conf ${pkgdir}/etc/calamares/settings.conf
    install -m644 data/artix/modules/*.conf ${pkgdir}/etc/calamares/modules/
}
