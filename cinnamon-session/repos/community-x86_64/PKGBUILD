# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-session
pkgver=4.0.0
pkgrel=1
pkgdesc="The Cinnamon Session Handler"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL' 'LGPL')
depends=('cinnamon-desktop' 'dbus-glib' 'libsm' 'libcanberra' 'xapps')
optdepends=('cinnamon-translations: i18n')
makedepends=('meson' 'xtrans')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
	"cinnamon-session-3.8.0-elogind2.patch"
	"cinnamon-session-3.8.0-elogind.patch"
	)
sha256sums=('cfd6d71ff4000f463b36ffa97ab415557d627776eefbe2720d8d954319a5bdc5'
            'e0a59cac803de9e5185497aae69838f23003c56e9c993dacc7015cee91c3dd45'
            'fd1d1c7f63510b91036510f51c4efbfa5dcd6e1ae2dbca7c47dd08c95b5e2461')

prepare() {
	cd $pkgname-$pkgver

	# patches here
	patch -Np 1 -i $srcdir/cinnamon-session-3.8.0-elogind2.patch
	patch -Np 1 -i $srcdir/cinnamon-session-3.8.0-elogind.patch
}

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib/${pkgname} \
          --buildtype=plain \
          -Dwith-gconf=false \
          -Dwith-docbook=false \
          ..
    ninja
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" ninja install
}

