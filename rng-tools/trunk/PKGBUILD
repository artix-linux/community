# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alasdair Haswell <ali at arhaswell dot co dot uk>

pkgname=rng-tools
pkgver=6.6
pkgrel=1
pkgdesc="Random number generator related utilities"
arch=('i686' 'x86_64')
url="https://github.com/nhorman/rng-tools"
license=('GPL')
depends=('libgcrypt' 'curl' 'libxml2' 'sysfsutils')
backup=(etc/conf.d/rngd)
source=(https://github.com/nhorman/rng-tools/archive/v$pkgver/$pkgname-$pkgver.tar.gz)

prepare() {
    cd "$srcdir"/$pkgname-$pkgver
    mv README{.md,}

    libtoolize --force
    aclocal
    autoheader
    automake --force-missing --add-missing
    autoconf
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure --prefix=/usr --sbindir=/usr/bin
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}

sha256sums=('201d15e9e5ae8468b14a7a2f3a7fa714c4e83380f4468b80893148bf700c2efe')
