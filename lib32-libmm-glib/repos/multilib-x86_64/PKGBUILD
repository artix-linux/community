# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-libmm-glib
pkgver=1.8.2
pkgrel=1
pkgdesc='ModemManager library'
arch=('x86_64')
url='https://www.freedesktop.org/wiki/Software/ModemManager/'
license=('GPL2' 'LGPL2.1')
depends=('lib32-glib2' 'libmm-glib')
makedepends=('git' 'intltool' 'lib32-libmbim' 'lib32-libqmi'
             'lib32-polkit' 'python' 'vala')
_commit=e89a4a6389cd05ee8f7323b382ac139a8d56768f # tags/1.8.2
source=("git+https://anongit.freedesktop.org/git/ModemManager/ModemManager#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd ModemManager

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ModemManager

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --localstatedir='/var' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --disable-gtk-doc-html \
    --disable-static \
    --with-dbus-sys-dir='/usr/share/dbus-1/system.d' \
    --with-polkit='permissive' \
    --with-suspend-resume=no \
    --with-udev-base-dir='/usr/lib32/udev'
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C include
  make -C libmm-glib
  make -C data
}

package() {
  make DESTDIR="${pkgdir}" -C ModemManager/libmm-glib install
  make DESTDIR="${pkgdir}" -C ModemManager/data install
  rm -rf "${pkgdir}"/{etc,usr/{include,lib,share}}
}

# vim: ts=2 sw=2 et:
